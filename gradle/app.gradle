import org.gradle.internal.os.OperatingSystem;

// use gradlew -Penv=${env} to pass
def environment = hasProperty('env') ? env : null

apply plugin: 'application'
mainClassName = 'Main'

def isWindows = OperatingSystem.current().isWindows();

clean {
    if (project.file("src/main/web").exists()) {
        doLast {
            exec {
                workingDir "${rootDir}/gradle/node"
                if (!isWindows) {
                    commandLine 'npm', 'install'
                } else {
                    commandLine 'cmd', '/c', 'npm', 'install'
                }
            }
            exec {
                workingDir "${rootDir}/gradle/node"
                if (!isWindows) {
                    commandLine 'npm', 'run', 'clean', '--', '--root', project.file('.')
                } else {
                    commandLine 'cmd', '/c', 'npm', 'run', 'clean', '--', '--root', project.file('.')
                }
            }
        }
    }
}

processResources {
    if (environment != null) {
        def envProperties = "conf/${environment}/resources"
        assert project.file(envProperties).exists()
        from envProperties
    }
    if (project.file("src/main/web").exists()) {
        doLast {
            exec {
                workingDir "${rootDir}/gradle/node"
                if (!isWindows) {
                    commandLine 'npm', 'install'
                } else {
                    commandLine 'cmd', '/c', 'npm', 'install'
                }
            }
            exec {
                workingDir "${rootDir}/gradle/node"
                if (!isWindows) {
                    commandLine 'npm', 'run', 'build', '--', '--root', project.file('.')
                } else {
                    commandLine 'cmd', '/c', 'npm', 'run', 'build', '--', '--root', project.file('.')
                }
            }
        }
    }
}

distTar {
    archiveName "${baseName}.${extension}"
}

startScripts {
    defaultJvmOpts = ["-Dcore.webPath=APP_HOME_VAR/web", "-Dcore.appName=${project.name}"]

    doLast {
        windowsScript.text = windowsScript.text.replaceFirst('APP_HOME_VAR', '%APP_HOME%')
        unixScript.text = unixScript.text.replaceFirst('APP_HOME_VAR', '\\$APP_HOME')
    }
}

distributions {
    main {
        if (environment != null) {
            def envProperties = "conf/${environment}/resources"
            assert project.file(envProperties).exists()
            contents {
                from("conf/${environment}/web") {
                    into "web"
                }
            }
        }
    }
}